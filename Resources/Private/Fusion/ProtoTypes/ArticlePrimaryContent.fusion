prototype(Neos.Neos:PrimaryContent).Article {
	@position = 'start'
	condition = ${q(documentNode).is('[instanceof UpAssist.Neos.News:Article]')}
	type = 'UpAssist.Neos.News:Article'
}

prototype(UpAssist.Neos.News:Article) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://UpAssist.Neos.News/Private/Templates/DocumentTypes/Article.html'

	node = ${node}
	title = ${q(node).property('title')}
	publicationDate = ${q(node).property('publicationDate')}

	breadcrumbs = UpAssist.Neos.News:BreadcrumbMenu

	main = Neos.Neos:ContentCollection {
		nodePath = 'main'
	}

	shareLinks = Neos.Fusion:RawArray {
		@override.link = Neos.Neos:NodeUri {
			absolute = TRUE
		}

		attributes = Neos.Fusion:Attributes {
			class = Neos.Fusion:RawArray {
				identifier = 'news-share-links'
				display = 'list-inline'
			}
		}

		links = Neos.Fusion:RawArray {
			twitter = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'https://twitter.com/home?status=' + link}
				content = '<i class="fa fa-twitter fa-lg"></i>'
			}

			facebook = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'https://www.facebook.com/sharer/sharer.php?u=' + link}
				content = '<i class="fa fa-facebook fa-lg"></i>'
			}

			googlePlus = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'https://plus.google.com/share?url=' + link}
				content = '<i class="fa fa-google-plus fa-lg"></i>'
			}

			linkedIn = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'https://www.linkedin.com/shareArticle?mini=true&url=' + link + '&title=&summary=&source='}
				content = '<i class="fa fa-linkedin fa-lg"></i>'
			}

			pinterest = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'https://pinterest.com/pin/create/button/?url=&media=' + link + '&description='}
				content = '<i class="fa fa-pinterest fa-lg"></i>'
			}

			email = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.href = ${'mailto:?&subject=&body=' + link}
				content = '<i class="fa fa-paper-plane fa-lg"></i>'
			}
		}
	}
}
